#Использовать fs
#Использовать ParserFileV8i

Перем Лог;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("C ib-connection ibconnection", "", "строка соединения с информационной базой")
				.ТСтрока()
				.ВОкружении("1COVERAGE_IB_CONNECTION 1COVERAGE_IBCONNECTION")
				.Обязательный(Истина);

	Команда.Опция("dbgs-host", "", "адрес сервера отладки")
				.ТСтрока()
				.ВОкружении("1COVERAGE_DBGS_HOST")
				.ПоУмолчанию("localhost");
				
	Команда.Опция("dbgs-port", "", "порт сервера отладки")
				.ТСтрока()
				.ВОкружении("1COVERAGE_DBGS_PORT")
				.ПоУмолчанию("1550");

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Лог = ПараметрыПриложения.Лог();

	// ПутьКФайлуНастроек = Команда.ЗначениеАргумента("settings");

	СтрокаСоединенияСИБ = Команда.ЗначениеОпции("ibconnection");
	СерверОтладки = Команда.ЗначениеОпции("dbgs-host");
	ПортСервераОтладки = Команда.ЗначениеОпции("dbgs-port");

	Если Не ЗначениеЗаполнено(СтрокаСоединенияСИБ) Тогда
		ВызватьИсключение("Не указана строка соединения с ИБ");
	КонецЕсли;

	ИдИнформационнойБазы = ОпределитьИдИнформационнойБазы(СтрокаСоединенияСИБ);

	AppData = ПолучитьПеременнуюСреды("APPDATA");
	КаталогФайлаНастроек = ОбъединитьПути(AppData, "1C", "1cv8", ИдИнформационнойБазы);
	ИмяФайлаНастроек = ОбъединитьПути(КаталогФайлаНастроек, "1cv8.pfl");

	ЗаменитьФайлСНастройкамиОтладки(ИмяФайлаНастроек, СерверОтладки, ПортСервераОтладки);

	Лог.Информация("Установлена http-отладка для базы, Ид " + ИдИнформационнойБазы);
	
КонецПроцедуры // ВыполнитьКоманду

Функция ОпределитьИдИнформационнойБазы(СтрокаСоединенияСИБ)

	ИдИнформационнойБазы = ОпределитьИдИнформационнойБазы_v8i(СтрокаСоединенияСИБ);

	Если Не ЗначениеЗаполнено(ИдИнформационнойБазы) Тогда
		ИдИнформационнойБазы = ОпределитьИдИнформационнойБазы_pfl(СтрокаСоединенияСИБ);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ИдИнформационнойБазы) Тогда
		ВызватьИсключение("Не удалось определить Ид информационной базы " + СтрокаСоединенияСИБ);
	КонецЕсли;

	Возврат ИдИнформационнойБазы;

КонецФункции

Функция ОпределитьИдИнформационнойБазы_v8i(СтрокаСоединенияСИБ)

	СтрокаСоединенияСИБ = СтрЗаменить(СтрокаСоединенияСИБ, "File=", "");
	Лог.Отладка("Поиск ИБ "+ СтрокаСоединенияСИБ + " в файле ibases.v8i");
	ИдИнформационнойБазы = "";

	Парсер = Новый ПарсерСпискаБаз;

	СписокИБ = Парсер.НайтиПоПути(СтрокаСоединенияСИБ);

	Если ТипЗнч(СписокИБ) = Тип("Структура") Тогда
		ИдИнформационнойБазы = СписокИБ.ID;
	Иначе
		Лог.Отладка("ИБ "+ СтрокаСоединенияСИБ + " не найдена в файле ibases.v8i");
	КонецЕсли;

	Возврат ИдИнформационнойБазы;

КонецФункции

Функция ОпределитьИдИнформационнойБазы_pfl(СтрокаСоединенияСИБ)

	// {"S","File=""N:\jenkins_workspace\workspace\AGPZ-EDT_APGZ-2656-P1\build\ib"";"},
	// {"S","7031c785-fd0b-4074-9868-0d0e9c70a9b3"},0},
	// {2,5,2,
	ИдБазы = "";

	СтрокаСоединенияСИБ = СтрЗаменить(СтрокаСоединенияСИБ, "File=", "");
	// TODO: server
	СтрокаСоединенияСИБ = СтрЗаменить(СтрокаСоединенияСИБ, ";", "");
	
	AppData = ПолучитьПеременнуюСреды("APPDATA");
	ИмяФайлаPFL = ОбъединитьПути(AppData, "1C", "1Cv8", "1cv8u.pfl");

	Лог.Отладка("Поиск ИБ "+ СтрокаСоединенияСИБ + " в файле " + ИмяФайлаPFL);

	Если Не ФС.ФайлСуществует(ИмяФайлаPFL) Тогда
		Лог.Отладка("Не найден файл" + ИмяФайлаPFL);
		Возврат ИдБазы;
	КонецЕсли;
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайлаPFL);

	ТекущаяСтрока = "";
	Пока ТекущаяСтрока <> Неопределено Цикл
		ТекущаяСтрока = ЧтениеТекста.ПрочитатьСтроку();

		Если СтрНайти(ТекущаяСтрока, СтрокаСоединенияСИБ) Тогда
			ИдБазы = ЧтениеТекста.ПрочитатьСтроку();
			ИдБазы = СтрЗаменить(ИдБазы, "{""S"",""", "");
			ИдБазы = СтрЗаменить(ИдБазы, """},0}", "");
			ИдБазы = СтрЗаменить(ИдБазы, ",", "");
			Лог.Отладка("В файле pfl найден Ид базы = " + ИдБазы);
			Прервать;
		КонецЕсли;

	КонецЦикла;
	ЧтениеТекста.Закрыть();

	Возврат ИдБазы;

КонецФункции

Процедура ЗаменитьФайлСНастройкамиОтладки(ИмяФайлаНастроекТекущейИБ, СерверОтладки, ПортСервераОтладки)

	ЧтениеТекста = Новый ЧтениеТекста("./fixtures/1cv8.pfl");
	СодержимоеФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	СодержимоеФайла = СтрЗаменить(СодержимоеФайла, "%COVERAGE_DEBUG_HOST%", СерверОтладки);
	СодержимоеФайла = СтрЗаменить(СодержимоеФайла, "%COVERAGE_DEBUG_PORT%", ПортСервераОтладки);

	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайлаНастроекТекущейИБ);
	ЗаписьТекста.Записать(СодержимоеФайла);
	ЗаписьТекста.Закрыть();
	
КонецПроцедуры
