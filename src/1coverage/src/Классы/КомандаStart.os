#Использовать 1commands
#Использовать fs

Перем Лог;

Перем КаталогВременныхФайлов;

Перем ПутьКПлатформе;
Перем СтрокаСоединенияСИБ;
Перем ИмяПользователяИБ;
Перем ПарольПользователяИБ;

Перем ХостСервераОтладки;
Перем ПортСервераОтладки;

Перем ПортПрокси;
Перем ПутьКФайлуЛога;

Перем PIDСервераОтладки;
Перем PIDПрокси;
Перем PIDКонфигуратора;

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("C ib-connection ibconnection", "", "строка соединения с информационной базой")
	.ТСтрока()
	.ВОкружении("1COVERAGE_IB_CONNECTION 1COVERAGE_IBCONNECTION")
	.Обязательный(Истина);
	
	Команда.Опция("U ib-user", "", "имя пользователя информационной базы")
	.ТСтрока()
	.ВОкружении("1COVERAGE_IB_USR 1COVERAGE_IB_USER");
	
	Команда.Опция("P ib-pwd", "", "пароль пользователя информационной базы")
	.ТСтрока()
	.ВОкружении("1COVERAGE_IB_PASSWORD 1COVERAGE_IB_PWD");
	
	Команда.Опция("dbgs-host", "", "адрес сервера отладки")
	.ТСтрока()
	.ВОкружении("1COVERAGE_DBGS_HOST")
	.ПоУмолчанию("localhost");
	
	Команда.Опция("dbgs-port", "", "порт сервера отладки")
	.ТСтрока()
	.ВОкружении("1COVERAGE_DBGS_PORT")
	.ПоУмолчанию("1550");
	
	Команда.Опция("proxy-host", "", "хост прокси")
	.ТСтрока()
	.ВОкружении("1COVERAGE_PROXY_HOST")
	.ПоУмолчанию("localhost");
	
	Команда.Опция("proxy-port", "", "порт прокси")
	.ТСтрока()
	.ВОкружении("1COVERAGE_PROXY_PORT")
	.ПоУмолчанию("3000");
	
	Команда.Опция("proxy-log", "", "путь к файлу лога прокси")
	.ТСтрока()
	.ВОкружении("1COVERAGE_PROXY_LOG")
	.ПоУмолчанию("./1coverage-dbgs-proxy.log");
	
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Лог = ПараметрыПриложения.Лог();

	ОбщиеПараметры = ПараметрыПриложения.Параметры();

	КаталогВременныхФайлов = Команда.ЗначениеОпции("tempdir");
	
	ПутьКПлатформе = Команда.ЗначениеОпции("v8path");
	СтрокаСоединенияСИБ = Команда.ЗначениеОпции("ib-connection");
	ИмяПользователяИБ = Команда.ЗначениеОпции("ib-user");
	ПарольПользователяИБ = Команда.ЗначениеОпции("ib-pwd");
	
	ХостСервераОтладки = Команда.ЗначениеОпции("dbgs-host");
	ПортСервераОтладки = Команда.ЗначениеОпции("dbgs-port");
	
	ПортПрокси = Команда.ЗначениеОпции("proxy-port");
	ПутьКФайлуЛога = Команда.ЗначениеОпции("proxy-log");
	
	Если Не ФС.ФайлСуществует(ПутьКФайлуЛога) Тогда
		ФайлЛога = Новый ЗаписьТекста(ПутьКФайлуЛога, КодировкаТекста.UTF8);
		ФайлЛога.ЗаписатьСтроку("");
		ФайлЛога.Закрыть();
	КонецЕсли;
	
	ЗапуститьСерверОтладки();
	ЗапуститьПрокси();
	ОткрытьКонфигуратор();
	
	ЗаписатьPIDПроцессовВФайл();
	
КонецПроцедуры

Процедура ЗапуститьСерверОтладки()
	
	ШаблонКоманды = """%1\dbgs.exe"" -a %2 -p %3";
	ТекстКоманды = СтрШаблон(ШаблонКоманды, ПутьКПлатформе, ХостСервераОтладки, ПортСервераОтладки);
	
	КонсольнаяКоманда = Новый Команда;
	КонсольнаяКоманда.УстановитьСтрокуЗапуска(ТекстКоманды);
	
	ПроцессСервераОтладки = КонсольнаяКоманда.ЗапуститьПроцесс();
	
	PIDСервераОтладки = ПроцессСервераОтладки.Идентификатор;
	Лог.Информация("Запущен сервер отладки 1С: " + PIDСервераОтладки);
	
КонецПроцедуры

Процедура ЗапуститьПрокси()
	
	ШаблонURL = "http://%1:%2";
	URLОтладчика = СтрШаблон(ШаблонURL, ХостСервераОтладки, ПортСервераОтладки);
	
	КонсольнаяКоманда = Новый Команда;
	
	ПеременныеСреды = Новый Соответствие();
	ПеременныеСреды.Вставить("DEBUGGER_URL", URLОтладчика);
	ПеременныеСреды.Вставить("PROXY_PORT", ПортПрокси);
	ПеременныеСреды.Вставить("PROXY_RESULT_DIR", ПутьКФайлуЛога);
	
	КонсольнаяКоманда.УстановитьПеременныеСреды(ПеременныеСреды);
	КонсольнаяКоманда.УстановитьСтрокуЗапуска("npm start");

	ПроцессПрокси = КонсольнаяКоманда.ЗапуститьПроцесс();

	PIDПрокси = ПроцессПрокси.Идентификатор;
	Лог.Информация("Запущен прокси: " + PIDПрокси);

КонецПроцедуры

Процедура ОткрытьКонфигуратор()
	
	КонсольнаяКоманда = Новый Команда;
	
	ШаблонКоманды = """%1\1cv8.exe"" DESIGNER %2";
	
	ТекстКоманды = СтрШаблон(ШаблонКоманды, ПутьКПлатформе, СтрокаСоединенияСИБ);
	
	Если ЗначениеЗаполнено(ИмяПользователяИБ) Тогда
		ТекстКоманды = ТекстКоманды + " /N" + ИмяПользователяИБ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПарольПользователяИБ) Тогда
		ТекстКоманды = ТекстКоманды + " /P" + ПарольПользователяИБ;
	КонецЕсли;
	
	КонсольнаяКоманда.УстановитьСтрокуЗапуска(ТекстКоманды);
	
	ПроцессКонфигуратора = КонсольнаяКоманда.ЗапуститьПроцесс();
	
	PIDКонфигуратора = ПроцессКонфигуратора.Идентификатор;
	Лог.Информация("Открыт конфигуратор 1С: " + PIDКонфигуратора);
	
КонецПроцедуры

Процедура ЗаписатьPIDПроцессовВФайл()
	
	ПутьКФайлуPID = ОбъединитьПути(КаталогВременныхФайлов, "PID");
	ФайлPID = Новый ЗаписьТекста(ПутьКФайлуPID, КодировкаТекста.UTF8);
	ФайлPID.ЗаписатьСтроку("" + PIDСервераОтладки + "|" + PIDПрокси + "|" + PIDКонфигуратора);
	ФайлPID.Закрыть();
	
КонецПроцедуры