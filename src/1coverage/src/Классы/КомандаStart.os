#Использовать 1commands

Перем Лог;

Перем ПутьКПлатформе;
Перем СтрокаСоединенияСИБ;
Перем ИмяПользователяИБ;
Перем ПарольПользователяИБ;

Перем ХостСервераОтладки;
Перем ПортСервераОтладки;
Перем ПутьКФайлуЛога;

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("C ib-connection ibconnection", "", "строка соединения с информационной базой")
	.ТСтрока()
	.ВОкружении("1COVERAGE_IB_CONNECTION 1COVERAGE_IBCONNECTION")
	.Обязательный(Истина);
	
	Команда.Опция("U ib-user", "", "имя пользователя информационной базы")
	.ТСтрока()
	.ВОкружении("1COVERAGE_IB_USR 1COVERAGE_IB_USER");
	
	Команда.Опция("P ib-pwd", "", "пароль пользователя информационной базы")
	.ТСтрока()
	.ВОкружении("1COVERAGE_IB_PASSWORD 1COVERAGE_IB_PWD");
	
	Команда.Опция("dbgs-host", "", "адрес сервера отладки")
	.ТСтрока()
	.ВОкружении("1COVERAGE_DBGS_HOST")
	.ПоУмолчанию("localhost");
	
	Команда.Опция("dbgs-port", "", "порт сервера отладки")
	.ТСтрока()
	.ВОкружении("1COVERAGE_DBGS_PORT")
	.ПоУмолчанию("3000");

	Команда.Опция("proxy-log", "", "путь к файлу лога")
	.ТСтрока()
	.ВОкружении("1COVERAGE_PROXY_LOG")
	.ПоУмолчанию("./build/1coverage/proxy.log");
	
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Лог = ПараметрыПриложения.Лог();
	
	ПутьКПлатформе = Команда.ЗначениеОпции("v8path");
	СтрокаСоединенияСИБ = Команда.ЗначениеОпции("ib-connection");
	ИмяПользователяИБ = Команда.ЗначениеОпции("ib-user");
	ПарольПользователяИБ = Команда.ЗначениеОпции("ib-pwd");

	ХостСервераОтладки = Команда.ЗначениеОпции("dbgs-host");
	ПортСервераОтладки = Команда.ЗначениеОпции("dbgs-port");
	ПутьКФайлуЛога = Команда.ЗначениеОпции("proxy-log");
	
	ЗапуститьСерверОтладки();
	ЗапуститьПрокси();
	ОткрытьКонфигуратор();
	
КонецПроцедуры

Процедура ЗапуститьСерверОтладки()
	
	ШаблонКоманды = """%1\dbgs.exe"" -a %2 -p %3";
	ТекстКоманды = СтрШаблон(ШаблонКоманды, ПутьКПлатформе, ХостСервераОтладки, ПортСервераОтладки);
	
	КонсольнаяКоманда = Новый Команда;
	КонсольнаяКоманда.УстановитьКоманду("start");
	КонсольнаяКоманда.ДобавитьПараметр("""dbgs-1coverage""");
	КонсольнаяКоманда.ДобавитьПараметр(ТекстКоманды);
	
	КодВозврата = КонсольнаяКоманда.Исполнить();

	Если КодВозврата = 0 Тогда
		Лог.Информация("Запущен сервер отладки 1С: " + КодВозврата);
	Иначе
		ВызватьИсключение("Не удалось запустить сервер отладки 1С");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапуститьПрокси()

	ШаблонURL = "http://%1:%2";
	URLОтладчика = СтрШаблон(ШаблонURL, ХостСервераОтладки, ПортСервераОтладки);
	
	КонсольнаяКоманда = Новый Команда;

	ПеременныеСреды = Новый Соответствие();
	ПеременныеСреды.Вставить("DEBUGGER_URL", URLОтладчика);
	ПеременныеСреды.Вставить("PROXY_PORT", ПортСервераОтладки);
	ПеременныеСреды.Вставить("PROXY_RESULT_DIR", ПутьКФайлуЛога); // TODO: обеспечить файл

	КонсольнаяКоманда.УстановитьПеременныеСреды(ПеременныеСреды);

	КонсольнаяКоманда.УстановитьКоманду("start");
	КонсольнаяКоманда.ДобавитьПараметр("""npm-coverage"""); // TODO: заголовок не устанавливается, остается "npm"
	КонсольнаяКоманда.ДобавитьПараметр("npm start");
	
	КодВозврата = КонсольнаяКоманда.Исполнить();

	Если КодВозврата = 0 Тогда
		Лог.Информация("Запущен прокси: " + КодВозврата);
	Иначе
		ВызватьИсключение("Не удалось запустить прокси");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьКонфигуратор()
	
	КонсольнаяКоманда = Новый Команда;

	ШаблонКоманды = """%1\1cv8.exe"" DESIGNER %2 /N%3 /P%4"; // TODO: если имя и пароль пустые

	ТекстКоманды = СтрШаблон(ШаблонКоманды, ПутьКПлатформе, СтрокаСоединенияСИБ, ИмяПользователяИБ, ПарольПользователяИБ);

	КонсольнаяКоманда.УстановитьКоманду("start");
	КонсольнаяКоманда.ДобавитьПараметр("""designer-1coverage""");
	КонсольнаяКоманда.ДобавитьПараметр(ТекстКоманды);
	
	КодВозврата = КонсольнаяКоманда.Исполнить();
	
	Если КодВозврата = 0 Тогда
		Лог.Информация("Открыт конфигуратор 1С: " + КодВозврата);
	Иначе
		ВызватьИсключение("Не удалось открыть конфигуратор 1С");
	КонецЕсли;

КонецПроцедуры
