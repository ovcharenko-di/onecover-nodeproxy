#Использовать 1commands
#Использовать 1connector

Перем Лог;

Перем КаталогВременныхФайлов;

Перем ХостПрокси;
Перем ПортПрокси;

Перем PIDСервераОтладки;
Перем PIDПрокси;
Перем PIDКонфигуратора;

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("proxy-host", "", "хост прокси")
	.ТСтрока()
	.ВОкружении("1COVERAGE_PROXY_HOST")
	.ПоУмолчанию("localhost");
	
	Команда.Опция("proxy-port", "", "порт прокси")
	.ТСтрока()
	.ВОкружении("1COVERAGE_PROXY_PORT")
	.ПоУмолчанию("3000");
	
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	Лог = ПараметрыПриложения.Лог();

	ОбщиеПараметры = ПараметрыПриложения.Параметры();

	КаталогВременныхФайлов = Команда.ЗначениеОпции("tempdir");
	
	ХостПрокси = Команда.ЗначениеОпции("proxy-host");
	ПортПрокси = Команда.ЗначениеОпции("proxy-port");
	
	ОстановитьЗамерПроизводительности();
	
	ПрочитатьPIDПроцессов();
	ЗакрытьКонфигуратор();
	ОстановитьСерверОтладки();
	ОстановитьПрокси();
	
КонецПроцедуры

Процедура ОстановитьЗамерПроизводительности()
	
	ПутьКШаблону = "./fixtures/bodySamples/debugoff.xml";
	Чтение = Новый ЧтениеТекста(ПутьКШаблону);
	Тело = Чтение.Прочитать();
	Чтение.Закрыть();
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("accept-charset", "utf-8");
	Заголовки.Вставить("content-type", "application/xml");
	
	АдресОтладчикаШаблон = "http://%1:%2";
	АдресОтладчика = СтрШаблон(АдресОтладчикаШаблон, ХостПрокси, ПортПрокси);
	
	Попытка
		Результат = КоннекторHTTP.Post(
				АдресОтладчика + "/e1crdbg/rdbg?cmd=setMeasureMode",
				Тело,
				,
				Новый Структура("Заголовки", Заголовки)
			);
		
		Сообщить(Результат.КодСостояния);
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

Процедура ПрочитатьPIDПроцессов()

	КоличествоPID = 3;

	ПутьКФайлуPID = ОбъединитьПути(КаталогВременныхФайлов, "PID");
	
	ФайлPID = Новый ЧтениеТекста(ПутьКФайлуPID);
	СтрокаPID = ФайлPID.ПрочитатьСтроку();
	ФайлPID.Закрыть();
	
	PIDПроцессов = СтрРазделить(СтрокаPID, "|");

	Если PIDПроцессов.Количество() <> КоличествоPID Тогда
		ВызватьИсключение("Некорректный список PID");
	КонецЕсли;
	
	PIDСервераОтладки = PIDПроцессов[0];
	PIDПрокси = PIDПроцессов[1];
	PIDКонфигуратора = PIDПроцессов[2];
	
КонецПроцедуры

Процедура ЗакрытьКонфигуратор()
	
	ЗавершитьПроцессОСПоPID(PIDКонфигуратора);
	
КонецПроцедуры

Процедура ОстановитьСерверОтладки()
	
	ЗавершитьПроцессОСПоPID(PIDСервераОтладки);
	
КонецПроцедуры

Процедура ОстановитьПрокси()
	
	ЗавершитьПроцессОСПоPID(PIDПрокси);
	
КонецПроцедуры

Процедура ЗавершитьПроцессОСПоPID(PID)
	
	КонсольнаяКоманда = Новый Команда;
	
	КонсольнаяКоманда.УстановитьКоманду("taskkill");
	КонсольнаяКоманда.ДобавитьПараметр("/F /PID " + PID + " /T");
	
	КодВозврата = КонсольнаяКоманда.Исполнить();
	
	Сообщить(КодВозврата);
	Сообщить(КонсольнаяКоманда.ПолучитьВывод());
	
КонецПроцедуры
